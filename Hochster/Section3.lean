import Mathlib.Algebra.Ring.Defs
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.RingTheory.Nilpotent.Defs
import Mathlib.RingTheory.Spectrum.Prime.Topology

import Hochster.Section2

open CategoryTheory PrimeSpectrum RingHom TopologicalSpace Topology

universe u

structure SpringCat where
  X : Type u
  tX : TopologicalSpace X
  A : Type u
  commRing : CommRing A
  isReduced : IsReduced A
  f : X â†’ PrimeSpectrum A
  isEmbedding : IsEmbedding f
  range_dense : Dense (Set.range f)
  range_isClosed : IsClosed (X := ConstructibleTop (PrimeSpectrum A)) (Set.range f)

namespace SpringCat

attribute [instance] SpringCat.tX SpringCat.commRing SpringCat.isReduced

@[ext]
structure Hom (ð”¸ ð”¹ : SpringCat) where
  hom' : ð”¹.A â†’+* ð”¸.A
  image_subset : hom'.specComap '' (Set.range ð”¸.f) âŠ† Set.range ð”¹.f

def Hom.id (ð”¸ : SpringCat) : Hom ð”¸ ð”¸ where
  hom' := RingHom.id ð”¸.A
  image_subset := by simp

def Hom.comp {ð”¸ ð”¹ â„‚ : SpringCat} (hom1 : ð”¸.Hom ð”¹) (hom2 : ð”¹.Hom â„‚) : ð”¸.Hom â„‚ where
  hom' := hom1.hom'.comp hom2.hom'
  image_subset := specComap_comp hom2.hom' hom1.hom' â–¸ Set.image_comp _ _ _ â–¸
    (Set.image_subset hom2.hom'.specComap hom1.image_subset).trans hom2.image_subset

instance : Category SpringCat where
  Hom ð”¸ ð”¹ := Hom ð”¸ ð”¹
  id ð”¸ := Hom.id ð”¸
  comp hom1 hom2 := hom1.comp hom2
  id_comp _ := rfl
  comp_id _ := rfl
  assoc _ _ _ := rfl

def isAffine (ð”¸ : SpringCat) := Set.range ð”¸.f = âŠ¤

instance (ð”¸ : SpringCat) : SpectralSpace ð”¸.X :=
  spectralSpace_of_isEmbedding_of_isClosed_constructibleTop_range ð”¸.isEmbedding ð”¸.range_isClosed

def inclusionRingHom (ð”¸ : SpringCat) :
    ð”¸.A â†’+* Î  x : ð”¸.X, ð”¸.A â§¸ (ð”¸.f x).asIdeal where
  toFun := fun a x => Ideal.Quotient.mk (ð”¸.f x).asIdeal a
  map_one' := by ext; simp
  map_mul' := fun _ _ => by ext; simp
  map_zero' := by ext; simp
  map_add' := fun _ _ => by ext; simp

lemma inclusionRingHom_injective (ð”¸ : SpringCat) :
    Function.Injective ð”¸.inclusionRingHom := by
  refine (RingHom.injective_iff_ker_eq_bot _).2 ?_
  Â· ext a
    simp only [inclusionRingHom, mem_ker, coe_mk, MonoidHom.coe_mk, OneHom.coe_mk]
    refine âŸ¨fun ha => by_contra fun hna => ?_, fun ha => ha â–¸ rflâŸ©
    Â· have h1 (x : ð”¸.X) : (Ideal.Quotient.mk (ð”¸.f x).asIdeal) a = 0 := by
        change (fun x => (Ideal.Quotient.mk (ð”¸.f x).asIdeal) a) x = 0
        exact ha â–¸ rfl
      have h2 : âˆƒ p : PrimeSpectrum ð”¸.A, a âˆ‰ p.asIdeal := by
        have : a âˆ‰ sInf { I : Ideal ð”¸.A | I.IsPrime } :=
          (nilradical_eq_sInf ð”¸.A â–¸ nilradical_eq_zero ð”¸.A) â–¸ hna
        simp only [Submodule.mem_sInf, not_forall] at this
        obtain âŸ¨I, hI, haIâŸ© := this
        use âŸ¨I, hIâŸ©
      obtain âŸ¨p, hapâŸ© := h2
      obtain âŸ¨q, hqa, x, hfxqâŸ© := Dense.inter_open_nonempty (ð”¸.range_dense)
        (PrimeSpectrum.basicOpen a).carrier (PrimeSpectrum.basicOpen a).is_open'
        (Set.nonempty_of_mem hap)
      have h3 : (Ideal.Quotient.mk (ð”¸.f x).asIdeal) a â‰  0 :=
        hfxq â–¸ fun hqa0 => hqa <| Ideal.Quotient.eq_zero_iff_mem.1 hqa0
      exact h3 <| h1 x

end SpringCat

structure SpringLike (X A : Type*) where
  tX : TopologicalSpace X
  spectralSpace : SpectralSpace X
  commRing : CommRing A
  i : X â†’ Type*
  forall_commRing (x : X) : CommRing (i x)
  forall_isDomain (x : X) : IsDomain (i x)
  h : A â†’+* Î  x : X, i x
  injective : Function.Injective h
  forall_eq_top (x : X) : { h a x | a : A } = âŠ¤
  forall_isOpen (a : A) : IsOpen { x : X | h a x â‰  0 }
  forall_isCompact (a : A) : IsCompact { x : X | h a x â‰  0 }
  isTopologicalBasis : IsTopologicalBasis { { x : X | h a x â‰  0 } | a : A }
